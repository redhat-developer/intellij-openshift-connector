
plugins {
    id "org.jetbrains.intellij" version "0.7.3"
    id "com.adarshr.test-logger" version "1.7.0"
    id "idea"
    id "java"
    id "jacoco"
    id "org.sonarqube" version "3.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repository.jboss.org' }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName 'org.jboss.tools.intellij.openshift'
    plugins 'java', 'terminal', 'JavaScriptDebugger', 'JavaScriptLanguage', 'CSS', 'yaml', 'com.redhat.devtools.intellij.telemetry:0.0.1.9', 'com.redhat.devtools.intellij.kubernetes:0.1.6.32'
	updateSinceUntilBuild false
}

//with this option enabled, build will fail about IDEA expiration builds
buildSearchableOptions.enabled = false

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

dependencies {
    compile 'io.fabric8:openshift-client:5.0.2'
    compile 'io.fabric8:servicecatalog-client:5.0.2'
    compile 'org.apache.commons:commons-compress:1.19'
    compile 'org.apache.commons:commons-exec:1.3'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'com.redhat.devtools.intellij:intellij-common:1.2.1'
    testCompile 'org.mockito:mockito-core:2.28.2'
    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile 'com.redhat.devtools.intellij:intellij-common:1.2.1:test'
}

def mergedCoverageFile = "$buildDir/jacoco/merged.exec"

sonarqube {
    properties {
        property "sonar.projectKey", "redhat-developer_intellij-openshift-connector"
        property "sonar.organization", "redhat-developer"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.jacoco.reportPaths", mergedCoverageFile
    }
}

jacocoTestReport {
    executionData = files(mergedCoverageFile)
    reports {
        xml.enabled = true
    }
    dependsOn tasks.jacocoMerge
}

task jacocoMerge(type: JacocoMerge) {
    executionData = files("$buildDir/jacoco/test.exec", "$buildDir/jacoco/integrationTest.exec")
    destinationFile = file(mergedCoverageFile)
    dependsOn test, integrationTest
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

runIdeForUiTests {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

group 'org.jboss.tools.intellij'
version projectVersion // Plugin version
