
plugins {
    id "org.jetbrains.intellij" version "0.4.7"
    id "com.adarshr.test-logger" version "1.7.0"
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'jacoco'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName 'org.jboss.tools.intellij.openshift'
    plugins 'terminal'
	updateSinceUntilBuild false
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

dependencies {
    compile 'io.fabric8:openshift-client:3.2.0'
    compile 'me.snowdrop:servicecatalog-client:1.0.1'
    compile 'org.apache.commons:commons-compress:1.19'
    compile 'org.apache.commons:commons-exec:1.3'
    testCompile 'org.mockito:mockito-core:2.23.4'
}

group 'org.jboss.tools.intellij'
version projectVersion // Plugin version
