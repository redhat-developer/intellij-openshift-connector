buildscript {
    ext.java_version = "17"
}

plugins {
    id 'org.jetbrains.intellij' version '1.17.4'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'idea'
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
}

repositories {
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
    mavenCentral()
    maven { url 'https://www.jetbrains.com/intellij-repository/snapshots' }
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies' }
}

configurations {
    compileOptions {
        sourceCompatibility = java_version
        targetCompatibility = java_version
    }
}

intellij {
    version = ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName = 'org.jboss.tools.intellij.openshift'
    // use '<git repo clone>/build/idea-sandbox/plugins/<plugin>' if working from source
    plugins = [
            'java',
            'terminal',
            'JavaScriptDebugger',
            'JavaScript',
            'com.intellij.css',
            'yaml',
            'com.redhat.devtools.intellij.telemetry:1.2.0.59',
            'com.redhat.devtools.intellij.kubernetes:1.3.0'
    ]
	updateSinceUntilBuild = false
}

//with this option enabled, build will fail about IDEA expiration builds
buildSearchableOptions.enabled = false

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token = jetBrainsToken
    channels = [jetBrainsChannel]
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

test {
    // Discover and execute JUnit4-based tests
    useJUnit()
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'disabled'
    jvmArgs "-Djava.awt.headless=true"
    jacoco {
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }
}

tasks.register('integrationTest', Test) {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'disabled'
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testlogger {
        showStandardStreams true
        showPassedStandardStreams false
        showSkippedStandardStreams false
        showFailedStandardStreams true
        showFullStackTraces true
    }
    jvmArgs "-Djava.awt.headless=true"
    jacoco {
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }
}
classpathIndexCleanup {
    dependsOn compileIntegrationTestJava
}

tasks.register('clusterIntegrationUITest', Test) {
    dependsOn copyKey
    useJUnitPlatform {
        includeTags 'ui-test'
    }
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'disabled'
    systemProperties['CLUSTER_ALREADY_LOGGED_IN'] = System.getenv('CLUSTER_ALREADY_LOGGED_IN') ?: 'false'
    description = 'Runs the cluster integration UI tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { true }
    testlogger {
        showStandardStreams true
        showPassedStandardStreams false
        showSkippedStandardStreams false
        showFailedStandardStreams true
        showFullStackTraces true
    }
    jvmArgs "-Djava.awt.headless=false" // use of clipboard in AboutPublicTest, set to false
    jacoco {
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }
    include '**/ClusterTestsSuite.class'
}

tasks.register('publicIntegrationUITest', Test) {
    dependsOn copyKey
    useJUnitPlatform {
        includeTags 'ui-test'
    }
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'disabled'
    description = 'Runs the public integration UI tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { true }
    testlogger {
        showStandardStreams true
        showPassedStandardStreams false
        showSkippedStandardStreams false
        showFailedStandardStreams true
        showFullStackTraces true
    }
    jvmArgs "-Djava.awt.headless=false" // use of clipboard in AboutPublicTest, set to false
    jacoco {
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }
    include '**/PublicTestsSuite.class'

}

tasks.register('copyKey', Copy) {
    from "idea_license_token/idea.key"
    into "build/idea-sandbox/config-uiTest"
}

dependencies {
    implementation(
            'io.fabric8:openshift-client:6.12.0',
            'org.apache.commons:commons-compress:1.26.2',
            'org.apache.commons:commons-exec:1.4.0',
            'com.redhat.devtools.intellij:intellij-common:1.9.6-SNAPSHOT',
            'io.jsonwebtoken:jjwt-impl:0.12.6',
            'io.jsonwebtoken:jjwt-jackson:0.12.6',
            'org.keycloak:keycloak-installed-adapter:24.0.5',
            'com.squareup.retrofit2:converter-jackson:2.11.0',
            'com.google.code.gson:gson:2.11.0')
    testImplementation(
            'org.junit.platform:junit-platform-launcher:1.10.3',
            'org.mockito:mockito-core:5.12.0',
            'org.easytesting:fest-assert:1.4',
            'com.redhat.devtools.intellij:intellij-common:1.9.6-SNAPSHOT:test',
            'org.awaitility:awaitility:4.2.1',
            'org.mock-server:mockserver-client-java:5.15.0',
            'org.mock-server:mockserver-netty:5.15.0',
            'com.redhat.devtools.intellij:intellij-common-ui-test-library:0.4.1',
            'org.junit.jupiter:junit-jupiter-engine:5.10.3',
            'org.junit.jupiter:junit-jupiter-api:5.10.3',
            'org.junit.jupiter:junit-jupiter:5.10.3',
            'org.junit.platform:junit-platform-suite:1.10.3')
    constraints {
        implementation('io.undertow:undertow-core:2.3.15.Final') { //keycloak
            because 'https://security.snyk.io/vuln/SNYK-JAVA-IOUNDERTOW-6567186'
        }
        implementation('org.bouncycastle:bcprov-jdk18on:1.78.1') { //keycloak
            because 'https://app.snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-6612984'
        }
        implementation('com.squareup.okhttp3:okhttp:4.12.0') { //retrofit
            because 'https://security.snyk.io/vuln/SNYK-JAVA-COMSQUAREUPOKHTTP3-2958044'
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "redhat-developer_intellij-openshift-connector"
        property "sonar.organization", "redhat-developer"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.junit.reportsPath", layout.buildDirectory.dir("test-results").get().asFile.absolutePath
        property "sonar.gradle.skipCompile", "true"
    }
}

jacocoTestReport {
    getExecutionData().setFrom(fileTree(layout.buildDirectory).include("/jacoco/*.exec"))
    reports {
        xml.required = true
    }
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
    //systemProperties['jboss.sandbox.api.endpoint'] = 'http://localhost:3000'
}

tasks.register('runSandbox', JavaExec) {
    group = "Execution"
    description = "Run the Sandbox registration server in port 3000"
    classpath = sourceSets.test.runtimeClasspath
    getMainClass().set 'org.jboss.tools.intellij.openshift.ui.sandbox.SandboxRegistrationServerMock'
}

group 'org.jboss.tools.intellij'
version projectVersion // Plugin version
